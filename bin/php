#!perl6

use v6;
use lib 'lib';

use PhP;

sub MAIN (
        $file?,           # <file> evaluate the code in a file and print the result
        :$e?,             # -e evaluate a string of code and print the result
        Bool :$c = False, # -c Just compile the source
        Bool :$d = False, # -d turn on debugging (just dumping extra info for now) 
        Bool :$v = False, # -v print out version information        
    ) {

    my %options = ( 
        version => PhP::VERSION,
        debug   => $d,
    );

    if $v {
        say %options<version>;
        exit;
    }

    my Str $source;

    if $e.defined {
        %options<file> = "-e";
        $source = ~ $e;
    }
    else {
        die "You must specify a file to execute" if ! $file.defined;
        %options<file> = $file;
        $source = slurp $file;
    }

    if $c {
        my $ast = PhP::parse( $source, %options );
        say ~ $ast if $d;
        say "Syntax OK" if $ast;  
        exit( $ast ?? 1 !! 0 );      
    }
    else {
        my $unit = PhP::run( $source, %options );
        say ~ $unit.root if $d;
        say ~ $unit.result;
        exit;
    }
}
